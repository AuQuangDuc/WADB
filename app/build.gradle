apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def localFile = rootProject.file('local.properties')
def localProps = new Properties()
def dontRestartAdbd
if (localFile.canRead()) {
    localProps.load(localFile.newDataInputStream())
    dontRestartAdbd = localProps.get("debug.dontRestartAdbd", false)
}

android {
    compileSdkVersion target_sdk
    defaultConfig {
        applicationId "moe.haruue.wadb"
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'de', 'id', 'it', 'ja', 'tr', 'zh-rCN', 'zh-rTW'
        buildConfigField "String", "GITHUB_URL", "\"https://github.com/RikkaApps/WADB\""
        buildConfigField "String", "LICENSE", "\"Apache License 2.0\""
        buildConfigField "String", "TRANSLATION_URL", "\"https://rikka.app/contribute_translation.html\""
        buildConfigField "String", "COPYRIGHT", "\"Copyright Â© Haruue Icymoon, PinkD, Rikka\""
        buildConfigField "boolean", "DONOT_RESTART_ADBD", "${dontRestartAdbd}" // set it true to prevent restart adbd (for debugging)
    }
    signingConfigs {
        sign
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
    }
    packagingOptions {
        exclude '/META-INF/*.version'
        exclude '/META-INF/*.version'
        exclude '/META-INF/*.kotlin_module'
        exclude '/kotlin/**'
    }
    lintOptions.checkReleaseBuilds false
    dependenciesInfo.includeInApk false
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "wadb-v${variant.versionName}.apk"

        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assembleProvider.get().doLast {
                copy {
                    from variant.mappingFile
                    into "release"
                    rename { String fileName ->
                        "mapping-${variant.versionCode}.txt"
                    }
                }
                copy {
                    from outputFile
                    into "release"
                }
            }
        }
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://dl.bintray.com/haruue/maven/' }
    maven { url 'https://dl.bintray.com/rikkaw/Libraries' }
    maven { url 'https://dl.bintray.com/rikkaw/Shizuku' }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
    exclude group: 'moe.shizuku.preference', module: 'preference-android'
    exclude group: 'moe.shizuku.preference', module: 'preference-simplemenu-android'
}

dependencies {
    def libsuVersion = '3.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    compileOnly project(':hidden')

    implementation 'com.google.android.material:material:1.2.0'

    implementation "androidx.core:core:$androidx_core_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_fragment_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_recyclerview_version"

    implementation "rikka.annotation:annotation:$rikka_annotation_version"
    implementation "rikka.appcompat:appcompat:$rikka_appcompat_version"
    implementation "rikka.core:core:$rikka_core_version"
    implementation "rikka.html:html-ktx:$rikka_html_version"
    implementation "rikka.material:material:$rikka_material_version"
    implementation "rikka.recyclerview:recyclerview-utils:$rikka_recyclerview_utils_version"

    implementation "moe.shizuku.preference:preference-appcompat:$rikka_preference_version"
    implementation "moe.shizuku.preference:preference-simplemenu-appcompat:$rikka_preference_version"

    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"

    implementation 'moe.shizuku.privilege:api:3.1.0-alpha3'
}


def signFile = rootProject.file('signing.properties')
def signProps = new Properties()
if (signFile.canRead()) {
    signProps.load(new FileInputStream(signFile))
    if (signProps != null) {
        android.signingConfigs.sign.storeFile file(signProps['KEYSTORE_FILE'])
        android.signingConfigs.sign.storePassword signProps['KEYSTORE_PASSWORD']
        android.signingConfigs.sign.keyAlias signProps['KEYSTORE_ALIAS']
        android.signingConfigs.sign.keyPassword signProps['KEYSTORE_ALIAS_PASSWORD']
    } else {
        println 'some entries in \'signing.properties\' not found!'
    }
} else {
    println '\'signing.properties\' not found!'
}

