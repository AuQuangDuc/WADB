import java.nio.file.Paths

plugins {
    id('com.android.application')
    id('kotlin-android')
    id('dev.rikka.tools.autoresconfig')
    id('dev.rikka.tools.materialthemebuilder')
}

def localFile = rootProject.file('local.properties')
def localProps = new Properties()
def dontRestartAdbd
if (localFile.canRead()) {
    localProps.load(localFile.newDataInputStream())
    dontRestartAdbd = localProps.get("debug.dontRestartAdbd", false)
}

def signFile = rootProject.file('signing.properties')
def signProps = new Properties()
def hasSignConfig = false
if (signFile.canRead()) {
    signProps.load(new FileInputStream(signFile))
    hasSignConfig = true
}

android {
    compileSdkVersion target_sdk
    ndkVersion "23.1.7779620"
    defaultConfig {
        applicationId "moe.haruue.wadb"
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'de', 'id', 'it', 'ja', 'pt-rBR', 'ru', 'tr', 'zh-rCN', 'zh-rTW'
        buildConfigField "String", "GITHUB_URL", "\"https://github.com/RikkaApps/WADB\""
        buildConfigField "String", "LICENSE", "\"Apache License 2.0\""
        buildConfigField "String", "TRANSLATION_URL", "\"https://rikka.app/contribute_translation/\""
        buildConfigField "String", "COPYRIGHT", "\"Copyright Â© Haruue Icymoon, PinkD, Rikka\""
        buildConfigField "boolean", "DONOT_RESTART_ADBD", "${dontRestartAdbd}" // set it true to prevent restart adbd (for debugging)
        setProperty("archivesBaseName", "wadb-v${versionName}")
        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments '-DANDROID_STL=none'
            }
        }
    }
    buildFeatures {
        prefab = true
    }
    signingConfigs {
        if (hasSignConfig) {
            sign {
                storeFile signProps.getProperty("KEYSTORE_FILE") != null ? file(signProps.getProperty("KEYSTORE_FILE")) : android.signingConfigs.debug.storeFile
                storePassword signProps.getProperty("KEYSTORE_PASSWORD", android.signingConfigs.debug.storePassword)
                keyAlias signProps.getProperty("KEYSTORE_ALIAS", android.signingConfigs.debug.keyAlias)
                keyPassword signProps.getProperty("KEYSTORE_ALIAS_PASSWORD", android.signingConfigs.debug.keyPassword)
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildTypes {
        debug {
            if (hasSignConfig) {
                signingConfig signingConfigs.sign
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (hasSignConfig) {
                signingConfig signingConfigs.sign
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    packagingOptions {
        jniLibs {
            excludes += ['/kotlin/**']
        }
        resources {
            excludes += ['/META-INF/*.version', '/META-INF/*.version', '/META-INF/*.kotlin_module', '/kotlin/**']
        }
    }
    dependenciesInfo.includeInApk false
    lint {
        checkReleaseBuilds false
    }
}

autoResConfig {
    generateClass = false
    generateRes = false
}

materialThemeBuilder {
    themes {
        wadb {
            primaryColor = "#88b984"
            dynamicColors = true
            lightThemeFormat = "Theme.Material3.Light.%s"
            lightThemeParent = "Theme.Material3.Light.Rikka"
            darkThemeFormat = "Theme.Material3.Dark.%s"
            darkThemeParent = "Theme.Material3.Dark.Rikka"
        }
        green {
            primaryColor = "#88b984"
            lightThemeFormat = "ThemeOverlay.%s.Light"
            lightThemeParent = "ThemeOverlay"
            darkThemeFormat = "ThemeOverlay.%s.Dark"
            darkThemeParent = "ThemeOverlay"
        }
        pink {
            primaryColor = "#F5A9B8"
            lightThemeFormat = "ThemeOverlay.%s.Light"
            lightThemeParent = "ThemeOverlay"
            darkThemeFormat = "ThemeOverlay.%s.Dark"
            darkThemeParent = "ThemeOverlay"
        }
    }
    generatePalette = true
    packageName = "moe.haruue.wadb"
}

def optimizeReleaseResources = task('optimizeReleaseResources').doLast {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
    def zip = Paths.get(project.buildDir.path, 'intermediates',
            'processed_res', 'release', 'out', "resources-release.ap_")
    def optimized = new File("${zip}.opt")
    def cmd = exec {
        commandLine aapt2, 'optimize', '--collapse-resource-names',
                '--shorten-resource-paths',
                '-o', optimized, zip
        ignoreExitValue false
    }
    if (cmd.exitValue == 0) {
        delete(zip)
        optimized.renameTo("$zip")
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'processReleaseResources') {
        task.finalizedBy optimizeReleaseResources
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assembleProvider.get().doLast {
                copy {
                    from variant.mappingFile
                    into "release"
                    rename { String fileName ->
                        "mapping-${variant.versionCode}.txt"
                    }
                }
                copy {
                    from outputFile
                    into "release"
                }
            }
        }
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
        content {
            includeGroup("com.github.topjohnwu.libsu")
        }
    }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {
    compileOnly project(':hidden')

    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation "dev.rikka.rikkax.appcompat:appcompat:1.4.1"
    implementation "dev.rikka.rikkax.core:core:1.3.4"
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'
    implementation 'dev.rikka.rikkax.material:material:2.1.1'
    implementation 'dev.rikka.rikkax.material:material-preference:1.0.0'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.3.1'
    implementation 'dev.rikka.rikkax.widget:borderview:1.1.0'
    implementation 'dev.rikka.rikkax.widget:mainswitchbar:1.1.0'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'

    def libsuVersion = '4.0.3'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"

    def shizuku_version = '12.0.0'
    implementation "dev.rikka.shizuku:api:${shizuku_version}"
}
